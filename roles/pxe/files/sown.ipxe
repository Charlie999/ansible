#!ipxe

set esc:hex 1b
set cls ${esc:string}[2J
set tty ttyS0

set sownversion 1.0.0+ (3fe68)


:menu
echo ${cls}
menu SOWN iPXE
item disk Boot from disk
item ubuntunoble Boot into ubuntu 24.04 installer
item tty Currently selected tty: ${tty}
item tty0 Use tty0 (physical display)
item ttyS0 Use ttyS0 (ipmi serial on old servers)
item ttyS1 Use ttyS1 (ipmi serial on newer servers)
item blank
item shell iPXE Shell
item settings iPXE Settings
item pxe Chainload iPXE
item self Reload
iseq ${version} ${sownversion} && set default disk || set default pxe
choose --timeout 10000 --default ${default} boot
goto ${boot}

:disk
exit 0

:ubuntu
kernel http://${next-server}/ubuntu-${version}-vmlinuz initrd=initrd.gz console=${tty},19200n8 ip=dhcp url=http://${next-server}/ubuntu-${version}-live-server-amd64.iso
initrd http://${next-server}/ubuntu-${version}-initrd
boot

:ubuntunoble
set version 24.04.1
goto ubuntu

:tty
goto menu

:tty0
set tty tty0
goto menu

:ttyS0
set tty ttyS0
goto menu

:ttyS1
set tty ttyS1
goto menu

:shell
shell
goto menu

:pxe
chain undionly.kpxe
goto menu

:settings
config
goto menu

:self
chain ${filename}
goto menu
